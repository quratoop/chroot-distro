#!/bin/awk -f
# awk-jq - Minimal jq for awk
# Usage:
#   awk -f awk-jq -v q='FILTER' file.json
#   awk -f awk-jq -v action='get_names' file.json
#   awk -f awk-jq -v action='get_value' -v distro='NAME' -v field='FIELD' file.json

BEGIN {
    if (!q && !action) q = "."
    json = ""
}

{ json = json $0 " " }

END {
    # Compact whitespace
    gsub(/[\n\r\t]+/, " ", json)
    gsub(/  +/, " ", json)
    gsub(/^ /, "", json)

    if (action == "get_names") {
        distros = getkey(json, "distros")
        i = 0
        while (1) {
            item = getidx(distros, i)
            if (item == "null") break

            name = getkey(item, "name")
            if (name != "null") {
                gsub(/^"|"$/, "", name)
                print name
            }
            i++
        }
    } else if (action == "get_value") {
        distros = getkey(json, "distros")
        i = 0
        found = 0
        while (1) {
            item = getidx(distros, i)
            if (item == "null") break

            name = getkey(item, "name")
            gsub(/^"|"$/, "", name)

            if (name == distro) {
                val = getkey(item, field)
                if (val != "null") {
                    # Handle array vs string
                    if (substr(val, 1, 1) == "[") {
                        # Convert array to space-separated string
                        gsub(/^\[|\]$/, "", val)
                        gsub(/,/, " ", val)
                        gsub(/"/, "", val)
                        gsub(/  +/, " ", val)
                        gsub(/^ | $/, "", val)
                        print val
                    } else {
                        gsub(/^"|"$/, "", val)
                        print val
                    }
                    found = 1
                    break
                }
            }
            i++
        }
        if (!found) exit 1
    } else {
        # Standard query support
        if (q == ".") {
            pretty(json)
        } else if (match(q, /^\.[a-z_][a-z0-9_]*\.[a-z_][a-z0-9_]*\.[a-z_][a-z0-9_]*$/)) {
            split(substr(q, 2), p, /\./)
            v = getkey(json, p[1])
            v = getkey(v, p[2])
            print getkey(v, p[3])
        } else if (match(q, /^\.[a-z_][a-z0-9_]*\.[a-z_][a-z0-9_]*$/)) {
            split(substr(q, 2), p, /\./)
            v = getkey(json, p[1])
            print getkey(v, p[2])
        } else if (match(q, /^\.[a-z_][a-z0-9_]*\[[0-9]+\]$/)) {
            match(q, /^\.([a-z_][a-z0-9_]*)/)
            k = substr(q, RSTART+1, RLENGTH-1)
            match(q, /\[([0-9]+)\]/)
            i = substr(q, RSTART+1, RLENGTH-2)
            v = getkey(json, k)
            print getidx(v, i)
        } else if (match(q, /^\.\[([0-9]+)\]$/)) {
            match(q, /\[([0-9]+)\]/)
            i = substr(q, RSTART+1, RLENGTH-2)
            print getidx(json, i)
        } else if (match(q, /^\.[a-z_][a-z0-9_]*$/)) {
            print getkey(json, substr(q, 2))
        } else {
            print "null"
        }
    }
}

function getkey(s, k,    i, d, q, e, p) {
    p = "\"" k "\" *: *"
    if (!match(s, p)) return "null"

    i = RSTART + RLENGTH
    return getval(s, i)
}

function getval(s, i,    c, r, d, q, e) {
    while (i <= length(s) && substr(s, i, 1) ~ / /) i++

    c = substr(s, i, 1)

    # String
    if (c == "\"") {
        r = "\""
        i++
        e = 0
        while (i <= length(s)) {
            c = substr(s, i, 1)
            r = r c
            if (!e && c == "\"") return r
            e = (!e && c == "\\")
            i++
        }
        return r
    }

    # Number
    if (c ~ /[-0-9]/) {
        r = ""
        while (i <= length(s) && substr(s, i, 1) ~ /[-0-9.eE+]/) {
            r = r substr(s, i, 1)
            i++
        }
        return r
    }

    # Keyword
    if (substr(s, i, 4) == "true") return "true"
    if (substr(s, i, 5) == "false") return "false"
    if (substr(s, i, 4) == "null") return "null"

    # Object
    if (c == "{") {
        d = 0
        q = 0
        e = 0
        r = ""
        while (i <= length(s)) {
            c = substr(s, i, 1)
            r = r c
            if (!q) {
                if (c == "{") d++
                else if (c == "}") {
                    d--
                    if (d == 0) return r
                }
            }
            if (c == "\"" && !e) q = !q
            e = (!e && c == "\\")
            i++
        }
        return r
    }

    # Array
    if (c == "[") {
        d = 0
        q = 0
        e = 0
        r = ""
        while (i <= length(s)) {
            c = substr(s, i, 1)
            r = r c
            if (!q) {
                if (c == "[") d++
                else if (c == "]") {
                    d--
                    if (d == 0) return r
                }
            }
            if (c == "\"" && !e) q = !q
            e = (!e && c == "\\")
            i++
        }
        return r
    }

    return "null"
}

function getidx(s, n,    i, d, q, e, c, cnt, st) {
    gsub(/^ /, "", s)
    if (substr(s, 1, 1) != "[") return "null"

    d = 0
    q = 0
    e = 0
    cnt = 0
    i = 2

    while (i <= length(s) && substr(s, i, 1) ~ / /) i++

    while (i <= length(s)) {
        c = substr(s, i, 1)

        if (cnt == n && d == 0) {
            st = i
            if (c == "\"") {
                r = "\""
                i++
                e = 0
                while (i <= length(s)) {
                    c = substr(s, i, 1)
                    r = r c
                    if (!e && c == "\"") return r
                    e = (!e && c == "\\")
                    i++
                }
            } else if (c == "{" || c == "[") {
                return getval(s, st)
            } else {
                r = ""
                while (i <= length(s) && substr(s, i, 1) ~ /[-0-9.eE+tfalsnu]/) {
                    r = r substr(s, i, 1)
                    i++
                }
                return r
            }
        }

        if (!q) {
            if (c == "[" || c == "{") d++
            else if (c == "]" || c == "}") {
                d--
                if (d < 0) break
            }
            else if (c == "," && d == 0) {
                cnt++
                i++
                while (i <= length(s) && substr(s, i, 1) ~ / /) i++
                continue
            }
        }

        if (c == "\"" && !e) q = !q
        e = (!e && c == "\\")
        i++
    }

    return "null"
}

function pretty(s,    i, n, c, q, e, r, prev) {
    n = 0
    q = 0
    e = 0
    r = ""

    for (i = 1; i <= length(s); i++) {
        c = substr(s, i, 1)
        prev = (i > 1) ? substr(s, i-1, 1) : ""

        if (c == "\"" && !e) q = !q

        if (!q) {
            if (c == "{" || c == "[") {
                r = r c "\n"
                n++
                r = r spaces(n)
            } else if (c == "}" || c == "]") {
                r = r "\n"
                n--
                r = r spaces(n) c
            } else if (c == ",") {
                r = r c "\n" spaces(n)
            } else if (c == ":") {
                r = r c " "
            } else if (c != " " || prev != ":") {
                r = r c
            }
        } else {
            r = r c
        }

        e = (!e && c == "\\")
    }

    print r
}

function spaces(n,    s, i) {
    s = ""
    for (i = 0; i < n; i++) s = s "  "
    return s
}
